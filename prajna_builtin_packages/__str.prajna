struct String{
    darray: DynamicArray<char>;
    size: i64;
}

implement operator::LinearIndex<char> for String {
    func Get(idx: i64)->char{
        return this.darray[idx];
    }

    func Set(idx: i64, value: char){
        this.darray[idx] = value;
    }
}

implement String{
    @static
    func Create(size: i64)->String{
        var str_re: String;
        str_re.darray = DynamicArray<char>::Create(size+1);
        str_re.size = size;
        return str_re;
    }

    @static
    func __from_char_ptr(cstr_ptr: __raw_ptr<char>)->String {
        var str_size = 0;
        while (cstr_ptr[str_size] != '\0'){
            str_size = str_size + 1;
        }
        var str_re = String::Create(str_size);
        for i in 0 to str_re.size {
            str_re.darray[i] = cstr_ptr[i];
        }

        return str_re;
    }

    func Print(){
        this.darray[this.size] = '\0';
        bindings::print_c(this.darray.data.raw_ptr);
    }

    func Append(v: char){
        this.darray.PushBack('\0');
        this.size = this.size + 1;
        this.darray[this.size-1] = v;
    }

    func PrintLine() {
        var tmp = this;
        tmp.Append('\n');
        tmp.Print();
    }

    func ToString()->String{
        return this;
    }
}

implement reference_count::ReferenceCountable for String {
    func Initialize(){
        this.size = 0;
        this.darray = DynamicArray<char>::Create(1);
        this.darray[0] = '\0';
    }

    func RegisterReferenceCount(){

    }

    func DecrementReferenceCount(){

    }

    func IncrementReferenceCount(){

    }
}


implement operator::Add<String> for String {
    func Add(str_re: String)->String{
        var new_str = String::Create(this.size + str_re.size);
        var i = 0;
        while (i < this.size){
            new_str.darray[i] = this.darray[i];
            i = i + 1;
        }
        var j = 0;
        while ( i < new_str.size ){
            new_str.darray[i] = str_re.darray[j];
            j = j + 1;
            i = i + 1;
        }
        // "hit11\n".Print();

        return new_str;
    }
}

implement operator::Equal<String> for String{
    func Equal(rhs_str: String) -> bool{
        if(this.size!=rhs_str.size) {
            return false;
        }
        var i=0;
        while(i<this.size){
            if(rhs_str.darray[i]!=this.darray[i]) {
                return false;
            }
            i = i + 1;
        }
        return true;
    }
}

implement operator::NotEqual<String> for String{
    func NotEqual(rhs_str: String) -> bool{
         return !(this == rhs_str);
    }
}

func Print(ss: String){
    ss.Print();
}

func PrintLine(ss: String) {
    ss.PrintLine();
}

